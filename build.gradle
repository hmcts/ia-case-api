
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("net.serenity-bdd:serenity-gradle-plugin:3.6.22")
        classpath("au.com.dius.pact:au.com.dius.pact.gradle.plugin:4.4.0-beta.1")
    }
}


plugins {
    id 'application'
    id 'checkstyle'
    id 'jacoco'
    id 'pmd'
    id 'com.github.ben-manes.versions' version '0.51.0'
    id "info.solidsoft.pitest" version '1.9.0'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.flywaydb.flyway' version '5.2.4'
    id 'org.owasp.dependencycheck' version '10.0.3'
    id 'org.sonarqube' version '4.3.0.3225'
    id 'org.springframework.boot' version '2.7.18'
    id 'uk.gov.hmcts.java' version '0.12.43'
    id 'au.com.dius.pact' version '4.2.14'
    id "io.freefair.lombok" version "6.6.3"
}

apply plugin: 'java'
apply plugin: 'net.serenity-bdd.serenity-gradle-plugin'
apply plugin: 'project-report'
apply plugin: 'idea'

def versions = [
        reformLogging      : '6.0.1',
        serenity           : '3.6.22',
        junit              : '5.8.2',
        junitPlatform      : '1.7.2',
        sonarPitest        : '0.5',
        pitest             : '1.9.0',
        restAssured        : '5.3.1'
]

ext.libraries = [
        junit5: [
                "org.junit.jupiter:junit-jupiter-api:${versions.junit}",
                "org.junit.jupiter:junit-jupiter-engine:${versions.junit}",
                "org.junit.jupiter:junit-jupiter-params:${versions.junit}",
                "org.junit.platform:junit-platform-commons:${versions.junitPlatform}",
                "org.junit.platform:junit-platform-engine:${versions.junitPlatform}"
        ]
]


mainClassName = 'uk.gov.hmcts.reform.iacaseapi.Application'
def jarName = 'ia-case-api.jar'

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation' << "-Xlint:unchecked" << "-Werror"
}

compileTestJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation' << "-Xlint:unchecked" << "-Werror"
}

tasks.withType(Test) {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'full'
    }
}

sourceSets {

    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }

    functionalTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/functionalTest/java')
        }
        resources.srcDir file('src/functionalTest/resources')
    }

    smokeTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/smokeTest/java')
        }
        resources.srcDir file('src/smokeTest/resources')
    }

    contractTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/contractTest/java')
        }
        resources.srcDir file('src/contactTest/resources')
    }
}

processIntegrationTestResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

processFunctionalTestResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

idea {
    module {
        testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
        testResourceDirs += project.sourceSets.integrationTest.resources.srcDirs
        testSourceDirs += project.sourceSets.functionalTest.java.srcDirs
        testResourceDirs += project.sourceSets.functionalTest.resources.srcDirs
        testSourceDirs += project.sourceSets.smokeTest.java.srcDirs
        testResourceDirs += project.sourceSets.smokeTest.resources.srcDirs
        testSourceDirs += project.sourceSets.contractTest.java.srcDirs
        testResourceDirs += project.sourceSets.contractTest.resources.srcDirs
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
    functionalTestImplementation.extendsFrom testImplementation
    functionalTestRuntime.extendsFrom testRuntime
    contractTestImplementation.extendsFrom testImplementation
    contractTestRuntime.extendsFrom testRuntime
}

checkstyle {
    maxWarnings = 0
    toolVersion = '10.12.0'
    // need to set configDirectory to rootDir otherwise submodule will use submodule/config/checkstyle
    configFile(new File(rootDir, 'config/checkstyle/checkstyle.xml'))
}

pmd {
    toolVersion = '7.0.0'
    ignoreFailures = true
    sourceSets = [sourceSets.main, sourceSets.test, sourceSets.integrationTest, sourceSets.functionalTest, sourceSets.smokeTest]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSets = [
            'category/java/errorprone.xml',
            'category/java/bestpractices.xml'
    ]
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {

    systemProperty "scenario", System.getProperty('scenario')
    systemProperty "file.encoding", "UTF-8"
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    finalizedBy aggregate
    testLogging {
        showStandardStreams = true
        showCauses = true
        showExceptions = true
        showStackTraces = true
    }
}

task smoke(type: Test, description: 'Runs the smoke tests.', group: 'Verification') {
    testClassesDirs = sourceSets.smokeTest.output.classesDirs
    classpath = sourceSets.smokeTest.runtimeClasspath
}

task contract(type: Test, description: 'Runs the consumer Pact tests', group: 'Verification') {
    useJUnitPlatform()
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
}

task fortifyScan(type: JavaExec, description: 'Run fortify scan.')  {
    mainClass = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
    classpath += sourceSets.test.runtimeClasspath
    jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

project.ext {
    pacticipant = 'ia_caseApi'
    pacticipantVersion = getCheckedOutGitCommitHash()
    set('springCloudVersion', "2021.0.3")
}

jacoco {
    toolVersion = "0.8.10"
}

jacocoTestReport {
    executionData(test, integration)
    reports {
        xml.required.set(true)
        csv.required.set(false)
        xml.outputLocation.set(layout.buildDirectory.file("reports/jacoco/test/jacocoTestReport.xml"))
    }
}

pitest {
    //print('./gradlew --version'.execute().text.trim())
    junit5PluginVersion = '1.1.2'
    targetClasses = ['uk.gov.hmcts.reform.iacaseapi.*']
    excludedClasses = [
            'uk.gov.hmcts.reform.iacaseapi.infrastructure.config.*',
            'uk.gov.hmcts.reform.iacaseapi.Application'
    ]
    threads = 10
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
    failWhenNoMutations = false
    verbose = true
    mutationThreshold = 85
}

project.tasks['pitest'].group = "Verification"

sonarqube {
    properties {
        property "sonar.projectName", "IA :: Immigration & Asylum case api"
        property "sonar.projectKey", "IACASEAPI"
        property "sonar.coverage.jacoco.xmlReportPaths", layout.buildDirectory.file("reports/jacoco/test/jacocoTestReport.xml").get().asFile
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"
        property "sonar.exclusions", "src/main/java/uk/gov/hmcts/reform/iacaseapi/infrastructure/config/**," +
                "src/main/java/uk/gov/hmcts/reform/iacaseapi/infrastructure/SystemDateProvider.java," +
                "src/main/java/uk/gov/hmcts/reform/iacaseapi/Application.java," +
                "src/main/java/uk/gov/hmcts/reform/iacaseapi/domain/handlers/presubmit/payment/AiPFeesHandler.java," +
                "src/main/java/uk/gov/hmcts/reform/iacaseapi/domain/handlers/presubmit/RequestFeeRemissionAipPreparer.java," +
                "src/main/java/uk/gov/hmcts/reform/iacaseapi/infrastructure/security/idam/IdamAuthoritiesConverter.java," +
                "src/main/java/uk/gov/hmcts/reform/iacaseapi/domain/entities/CaseFlag.java"
    }
}

project.tasks['sonarqube'].dependsOn test, integration, jacocoTestReport
project.tasks['sonarqube'].group = "Verification"

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }

    dependencies {

        dependencySet(group: 'com.google.guava', version: '32.1.2-jre') {
            entry 'guava'
        }

        dependencySet(group: 'org.yaml', version: '2.0') {
            entry 'snakeyaml'
        }

        dependencySet(group: 'commons-fileupload', version: '1.5') {
            entry 'commons-fileupload'
        }

        dependencySet(group: 'com.fasterxml.jackson.core', version: '2.16.0') {
            entry 'jackson-databind'
            entry 'jackson-core'
            entry 'jackson-annotations'
        }

        dependencySet(group: 'com.fasterxml.jackson.dataformat', version: '2.16.0') {
            entry 'jackson-dataformat-csv'
            entry 'jackson-dataformat-yaml'
        }

        dependencySet(group: 'com.fasterxml.jackson.datatype', version: '2.16.0') {
            entry 'jackson-datatype-jsr310'
            entry 'jackson-datatype-jdk8'
        }

        dependencySet(group: 'com.fasterxml.jackson.module', version: '2.16.0') {
            entry 'jackson-module-parameter-names'
        }
    }
}

dependencies {
    modules {
        module("org.codehaus.groovy:groovy") {
            replacedBy("org.apache.groovy:groovy", "conflicts in current rest-assured version")
        }
        module("org.codehaus.groovy:groovy-xml") {
            replacedBy("org.apache.groovy:groovy-xml", "conflicts in current rest-assured version")
        }
        module("org.codehaus.groovy:groovy-json") {
            replacedBy("org.apache.groovy:groovy-json", "conflicts in current rest-assured version")
        }
    }
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation group: 'org.springframework.security', name: 'spring-security-oauth2-client'
    implementation group: 'org.springframework.security', name: 'spring-security-oauth2-resource-server'
    implementation group: 'org.springframework.security', name: 'spring-security-oauth2-jose'

    implementation group: 'org.springframework.security', name: 'spring-security-test'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-cache'
    implementation group: 'org.springframework', name: 'spring-context-support'
    implementation group: 'com.github.ben-manes.caffeine', name: 'caffeine'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind'
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310'
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-csv'
    implementation group: 'org.flywaydb', name: 'flyway-core'
    implementation group: 'org.yaml', name: 'snakeyaml'
    implementation group: 'org.apache.commons', name: 'commons-lang3'

    implementation group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '9.22'
    implementation (group: 'net.minidev', name: 'json-smart', version: '2.4.10'){
        version {
            strictly('2.4.10')
        }
    }
    implementation group: 'commons-io', name: 'commons-io', version: '2.10.0'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient'
    implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.6.8'
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.4.4'
    implementation group: 'com.sun.xml.bind', name: 'jaxb-osgi', version: '2.3.3'

    implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: '9.0.83'
    implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-websocket', version: '9.0.83'

    implementation group: 'com.google.guava', name: 'guava'
    implementation group: 'com.launchdarkly', name: 'launchdarkly-java-server-sdk', version: '6.2.1'

    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.13'
    implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.2.13'
    implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '8.0'

    implementation group: 'org.javamoney.moneta', name: 'moneta-core', version: '1.4.2'

    implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: versions.reformLogging
    implementation group: 'com.github.hmcts.java-logging', name: 'logging-appinsights', version: versions.reformLogging
    implementation(group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '4.0.2') {
        exclude group: "org.bouncycastle", module: "bcprov-jdk15on"
    }
    implementation(group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '3.1.8') {
        exclude group: "org.bouncycastle", module: "bcprov-jdk15on"
    }
    implementation(group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap' , version: '3.1.7') {
        exclude group: "org.bouncycastle", module: "bcprov-jdk15on"
    }
    implementation group: 'org.bouncycastle', name: 'bcprov-jdk18on', version: '1.74'
    implementation group: 'uk.gov.service.notify', name: 'notifications-java-client', version: '5.0.1-RELEASE'

    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
    testCompileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.30'

    implementation group: 'org.springframework.retry', name: 'spring-retry', version: '1.3.3'
    implementation group: 'org.springframework.security', name: 'spring-security-crypto'

    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations'

    implementation(group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap' , version: '4.0.0'){
        exclude group: "org.bouncycastle", module: "bcprov-jdk15on"
    }

    implementation group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '9.37.3'

    implementation group: 'net.minidev', name: 'json-smart', version: '2.5.0'
    implementation 'joda-time:joda-time:2.12.7'

    implementation group: 'com.googlecode.libphonenumber', name: 'libphonenumber', version: '8.13.43'

    testImplementation group: 'org.pitest', name: 'pitest', version: versions.pitest
    testImplementation "info.solidsoft.gradle.pitest:gradle-pitest-plugin:${versions.pitest}"
    testImplementation "org.codehaus.sonar-plugins:sonar-pitest-plugin:${versions.sonarPitest}"

    testImplementation group: 'com.h2database', name: 'h2', version: '1.4.197'

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'

    testImplementation group: 'org.mockito', name: 'mockito-core', version :'4.3.0'
    testImplementation group: 'org.mockito', name: 'mockito-inline', version :'5.2.0'

    testImplementation(group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock', version: '3.1.6') {
        exclude group: "com.github.tomakehurst", module: "wiremock-standalone"
    }
    testImplementation group: 'org.powermock', name: 'powermock-api-mockito2', version: '2.0.9'

    testImplementation(group: 'io.rest-assured', name: 'rest-assured-all', version: versions.restAssured)
    testImplementation group: 'io.rest-assured', name: 'rest-assured', version: versions.restAssured

    testImplementation group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity
    testImplementation group: 'net.serenity-bdd', name: 'serenity-junit', version: versions.serenity
    testImplementation group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity
    testImplementation group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity

    testImplementation group: 'com.github.hmcts', name: 'fortify-client', version: '1.3.0', classifier: 'all', {
        exclude group: 'commons-io', module: 'commons-io'
        exclude group: 'org.apache.commons', module: 'commons-lang3'
    }

    testImplementation group: 'com.github.hmcts', name: 'document-management-client', version: '7.0.1'
    testImplementation group: 'com.github.hmcts', name: 'ccd-case-document-am-client', version: '1.7.3'
    testImplementation group: 'org.apache.httpcomponents', name: 'fluent-hc', version:  '4.5.14'


    functionalTestImplementation group: 'com.github.hmcts', name: 'core-case-data-store-client', version: '4.9.2'


    //pact contract testing
    contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'junit5', version: '4.2.14'
    contractTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
    contractTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    contractTestImplementation group: 'com.github.hmcts', name: 'document-management-client', version: '7.0.1'
    contractTestRuntime group: 'com.github.hmcts', name: 'document-management-client', version: '7.0.1'

    contractTestImplementation sourceSets.main.runtimeClasspath
    contractTestImplementation sourceSets.test.runtimeClasspath

    integrationTestImplementation sourceSets.main.runtimeClasspath
    integrationTestImplementation sourceSets.test.runtimeClasspath

    functionalTestImplementation sourceSets.main.runtimeClasspath
    functionalTestImplementation sourceSets.test.runtimeClasspath

    smokeTestImplementation sourceSets.main.runtimeClasspath
    smokeTestImplementation sourceSets.test.runtimeClasspath

    contractTestImplementation sourceSets.main.runtimeClasspath
    contractTestImplementation sourceSets.test.runtimeClasspath

}

task runAndPublishConsumerPactTests(type: Test) {
    logger.lifecycle("Runs pact Tests")
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath

}

runAndPublishConsumerPactTests.finalizedBy pactPublish

pact {
    broker {
        pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'http://localhost:80'
    }
    publish {
        pactDirectory = 'pacts'
        tags = [System.getenv("PACT_BRANCH_NAME") ?: 'Dev']
        version = project.pacticipantVersion
    }
}

dependencyCheck {
    suppressionFile = 'config/owasp/suppressions.xml'
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

def getCheckedOutGitCommitHash() {
    'git rev-parse --verify --short HEAD'.execute().text.trim()
}

gradle.startParameter.continueOnFailure = true

bootJar {
    getArchiveFileName().set(jarName)
    manifest {
        attributes('Implementation-Version': project.version.toString())
    }
}

// this is required to force Java running on the Azure Windows Server OS into using
// UTF-8 as the default character set instead of windows-1252 which causes issues.
// this setting only applies when running via gradle bootRun -- see web.config for the
// java flag that configures the deployed appllications
applicationDefaultJvmArgs = ["-Dfile.encoding=UTF-8"]

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
